!-------------------------------------------------------------------------------
! Name:
!    Read_SwRTM_nc
!
! Purpose:
!    Reads the shortwave Radiative Transfer Model (atmospheric) file and loads
!    appropriate data arrays.
!
! Arguments:
!    Name   Type         In/Out/Both Description
!    Ctrl   struct       Both        Control structure
!    RTM    alloc struct Out         RTM structure
!    verbose logical      In          Print out progress information
!
! Algorithm:
! 1) Open the SWRTM file.
! 2) Read the instrument name and ensure it matches that expected.
! 3) Read the available channel numbers.
! 4) Read the necessary solar channel information.
! 5) Close the SWRTM file.
!
! History:
!     5th Dec 2000, Kevin M. Smith:
!       Original version
!    15th Jan 2001, Kevin M. Smith:
!       Changed Ctrl%Ind%Y to Ctrl%Ind%Y_Id
!    17th Jan 2001, Kevin M. Smith:
!       Corrected indexing of RTM%SW%Lat and Lon from 1-D to 2-D array
!    25th Jan 2001, Kevin M. Smith:
!       Corrected calculation of LatN and LonN in RTM%SW%Grid.
!    21st Feb 2001, Andy Smith:
!       Added Tbc to SW structure. Previously missing from model data.
!     1st Mar 2001, Andy Smith:
!       Removed allocation of R_Clear in SW RTM struct. R_Clear not available
!       from RTM data file.
!    30th Mar 2001, Andy Smith:
!       Added setting of new grid variables MaxLat, MinLat, MaxLon, MinLon.
!       Avoids repeated re-calculation in GetRTM.
!    22nd Jun 2001, Andy Smith:
!       Updated header comments and added argument intent.
!    24th Oct 2001, Andy Smith:
!       Added deallocation of local allocatable arrays.
!       Removed change of sign on longitude values. Data should be supplied on a
!       grid with west -ve.
!    *** ECV work starts here ***
!     7th Feb 2011, Andy Smith:
!       Re-applying changes made in 2002.
!       Converted to unformatted read:
!       - Variables x, y, buf and bufe are declared as real(4) in order to match
!         the number of bytes used for reals in the RTM files (previously 8).
!       - Allocation of buffer array "buf" changed from 7 parameters to 5, since
!         whole array writes to binary file mean that the level and channel
!         indices are no longer present.
!       - Array dimensions in buf swapped round to make access more efficient.
!         Channel is now the first index, then pressure level, and parameter last.
!       Error checking improved: iostat values checked.
!       Added tests for allocation status before deallocation of local
!       allocatable arrays (may not be allocated if errors detected before
!       allocation).
!       Date changed to character length 8 (YYYYMMDD) instead of 10.
!    19th Sep 2002, Caroline Poulsen, bug found, changed the deltalat and
!       deltalon
!    12th Dec 2002, Caroline Poulsen now read geopotential height from profile
!       file)
!    15th Feb 2011, Andy Smith:
!       Character string "dummy" length changed from 10 to 8, otherwise read
!       error occurs on prtm file.
!    22nd Sep 2011, Caroline Poulsen: modified the lwrtm code to read in the
!       shortwave rtm which is now scene dependant and not fixed for each
!       latitude band.
!    20th Jan 2012:
!       General tidy up
!    2012/08/23, MJ: Uses initial file as template for netcdf read.
!    2012/08/28, CP: Defined nchan and changed indicing of y_id
!    2012/11/03, MJ: Changed loop over SW channels
!    2013/01/01, MJ: Irones out some bugs wrt old binary file implementation.
!    2014/04/18, GM: Made reading of NetCDF input more efficient by avoiding
!       inefficient access patterns and redundancy and cleaned up the code.
!    2014/05/28, GM: Removed unused read of attribute 'Product_Date'.
!    2014/07/23, AP: Commented out unused code for future deletion.
!    2014/08/15, AP: Switching to preprocessor NCDF routines.
!    2014/09/18, AP: Update to RTTOV11 output arrays in the correct shape.
!    2014/09/28, GM: Updated to conform with a new arrangement of dimensions.
!    2014/12/19, AP: YSolar and YThermal now contain the index of solar/thermal
!       channels with respect to the channels actually processed, rather than the
!       MSI file.
!    2015/03/11, GM: Do not read wavelength dependent fields if NSolar is
!       equal to 0.
!
! Bugs:
!    None known.
!
! $Id$
!
!-------------------------------------------------------------------------------

subroutine Read_SwRTM_nc(Ctrl, RTM, verbose)

   use CTRL_def
   use ECP_Constants
   use orac_ncdf

   implicit none

   ! Argument declarations

   type(CTRL_t), intent(in)    :: Ctrl
   type(RTM_t),  intent(out)   :: RTM
   logical,      intent(in)    :: verbose

   ! Local variables

   ! Note on values read from the binary Lw and P RTM files. These files are
   ! generated by RTTOV code, which is compiled with a flag to force reals to
   ! become real(8). The parameter arrays read in via buf, and the lat/lons
   ! etc are explicitly written as real(4) in order to reduce the file size.

   integer                  :: ncid, chan_found, i, j
   character(Instnamelen)   :: platform, sensor, instname
   integer, allocatable     :: index(:), ChanID(:)
!  real(4), allocatable     :: WvNumber(:)


   !----------------------------------------------------------------------------
   ! Read SwRTM file
   !----------------------------------------------------------------------------

   ! Open RTM data file
   call nc_open(ncid, Ctrl%Fid%SWRTM)

   ! Ensure instrument info matches the sensor being processed
   if (nf90_get_att(ncid, NF90_GLOBAL, "Sensor", sensor) /= NF90_NOERR .or.&
       nf90_get_att(ncid, NF90_GLOBAL, "Platform", platform) /= NF90_NOERR) then
      write(*,*) 'ERROR: Read_SwRTM_nc(): Could not read global attributes: ', &
                 Ctrl%FID%LWRTM
      stop error_stop_code
   end if
   if (sensor =='AATSR') then
      instname=trim(adjustl(sensor))
   else
      instname=trim(adjustl(sensor))//'-'//trim(adjustl(platform))
   end if
   if (trim(adjustl(instname)) /= trim(adjustl(Ctrl%Inst%Name))) then
      write(*,*) 'ERROR: Read_SwRTM_nc(): Instrument in LWRTM header inconsistent: ', &
                 trim(adjustl(instname)), ' /= ', trim(adjustl(Ctrl%Inst%Name))
      stop error_stop_code
   end if

   allocate(ChanID(RTM%SW%NSWF))
!  allocate(WvNumber(RTM%SW%NSWF))

   ! Read ChanID and WvNumber
   call nc_read_array(ncid, "sw_channel_instr_ids", ChanID, verbose)
!  call nc_read_array(ncid, "sw_channel_wvl", WvNumber, verbose)

   if (verbose) write(*,*) &
      'SW channel instrument ids for RTM in SW preprocessing file',ChanID

   ! Check that required solar channels are present

   ! Loop over longwave instrument channels, checking that requested
   ! channels are available in the RTM data and setting up the index
   ! array to allow us to find the channels we want from the RTM file
   ! data (in case order of storage is different from the order we
   ! specified our selection).

   chan_found = 0
   allocate(index(Ctrl%Ind%NSolar))
   index = 0

   ! This is the loop over the requested channels
   do i = 1,Ctrl%Ind%Ny
      ! Loop over channels in RTM
      do j = 1,RTM%SW%NSWF
         ! Signal that the required channel has been found by incrementing
         ! chan_found and break out of the inner loop to start search for
         ! next instrument channel
         if (Ctrl%Ind%Y_Id(i) == ChanID(j)) then
            chan_found = chan_found + 1
            index(chan_found) = j
            exit
         end if
      end do
   end do

   if (chan_found /= Ctrl%Ind%NSolar) then
      write(*,*) 'ERROR: Read_SwRTM_nc(): Required instrument channels not ' // &
                 'found in: ', Ctrl%FID%LWRTM
      stop error_stop_code
   end if

   if (Ctrl%Ind%NSolar > 0) then
      ! Allocate arrays
      allocate(RTM%SW%Tbc(Ctrl%Ind%NSolar, RTM%SW%NP, RTM%SW%Grid%NLon, &
         RTM%SW%Grid%NLat))
      allocate(RTM%SW%Tac(Ctrl%Ind%NSolar, RTM%SW%NP, RTM%SW%Grid%NLon, &
         RTM%SW%Grid%NLat))

      call nc_read_array(ncid, "tac_sw", RTM%SW%Tac, verbose, 1, index)
      call nc_read_array(ncid, "tbc_sw", RTM%SW%Tbc, verbose, 1, index)
   end if

   ! Close SwRTM input file
   if (nf90_close(ncid) /= NF90_NOERR) then
      write(*,*) 'ERROR: Read_SwRTM_nc(): Error closing SWRTM file: ', &
                 Ctrl%FID%LWRTM
      stop error_stop_code
   end if

   if (allocated(index)) deallocate(index)
!  if (allocated(WvNumber)) deallocate(WvNumber)
   if (allocated(ChanID)) deallocate(ChanID)

end subroutine Read_SwRTM_nc
