!-------------------------------------------------------------------------------
! Name: ReadPRTM_nc.F90
!
! Purpose:
! Reads the profile Radiative Transfer Model (atmospheric) file and loads
! appropriate data arrays.
!
! Description and Algorithm details:
! 1) Open the PRTM file.
! 2) Read wavelength-independent fields from the PRTM file.
! 3) Close the PRTM file.
!
! Arguments:
! Name    Type         In/Out/Both Description
! ------------------------------------------------------------------------------
! Ctrl    struct       Both        Control structure
! RTM     alloc struct Out         RTM structure
!
! History:
! 2014/04/27, AP: Original version forked from ReadLwRTM_nc.F90.
! 2015/09/07, AP: Allow verbose to be controlled from the driver file.
!
! $Id$
!
! Bugs:
! None known.
!-------------------------------------------------------------------------------

subroutine Read_PRTM_nc(Ctrl, RTM)

   use CTRL_def
   use ECP_Constants
   use orac_ncdf

   implicit none

   ! Argument declarations

   type(CTRL_t), intent(in)  :: Ctrl
   type(RTM_t),  intent(out) :: RTM

   ! Local variables

   ! Note on values read from the binary Lw and P RTM files. These files are
   ! generated by RTTOV code, which is compiled with a flag to force reals to
   ! become real(8). The parameter arrays read in via buf, and the lat/lons
   ! etc are explicitly written as real(4) in order to reduce the file size.

   integer                  :: ncid, i
   real(sreal), allocatable :: dummy1d(:)


   ! Open PRTM file
   call nc_open(ncid, Ctrl%FID%PRTM)

   ! Allocate arrays
   allocate(RTM%LW%lat(RTM%LW%Grid%NLon, RTM%LW%Grid%NLat))
   allocate(RTM%LW%lon(RTM%LW%Grid%NLon, RTM%LW%Grid%NLat))

   allocate(RTM%LW%P(RTM%LW%NP, RTM%LW%Grid%NLon, RTM%LW%Grid%NLat))
   allocate(RTM%LW%T(RTM%LW%NP, RTM%LW%Grid%NLon, RTM%LW%Grid%NLat))
   allocate(RTM%LW%H(RTM%LW%NP, RTM%LW%Grid%NLon, RTM%LW%Grid%NLat))

   ! Read data into arrays
   allocate(dummy1d(RTM%LW%Grid%NLon))
   call nc_read_array(ncid, "lon_rtm", dummy1d, Ctrl%verbose)
   do i=1,RTM%LW%Grid%NLon
      RTM%LW%lon(i,:) = dummy1d(i)
   end do
   deallocate(dummy1d)

   allocate(dummy1d(RTM%LW%Grid%NLat))
   call nc_read_array(ncid, "lat_rtm", dummy1d, Ctrl%verbose)
   do i=1,RTM%LW%Grid%NLat
      RTM%LW%lat(:,i) = dummy1d(i)
   end do
   deallocate(dummy1d)

   call nc_read_array(ncid, "pprofile_rtm", RTM%LW%P, Ctrl%verbose)
   call nc_read_array(ncid, "tprofile_rtm", RTM%LW%T, Ctrl%verbose)
   call nc_read_array(ncid, "hprofile_rtm", RTM%LW%H, Ctrl%verbose)

   ! Close PRTM input file
   if (nf90_close(ncid) /= NF90_NOERR) then
      write(*,*) 'ERROR: Read_LwRTM_nc(): Error closing PRTM file: ', &
                 Ctrl%FID%PRTM
      stop error_stop_code
   end if

end subroutine Read_PRTM_nc
